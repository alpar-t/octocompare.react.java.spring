buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.4.1.RELEASE")
    }
}


apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'spring-boot'

compileJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    options.compilerArgs  << '-parameters'
}

version =  '0.1.0'

repositories {
    mavenCentral()
}

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integration-test/java')
        }
        resources.srcDir file('src/integration-test/resources')
    }
}

dependencies {
    compile("org.springframework.boot:spring-boot-starter-web") {
        exclude module: "spring-boot-starter-tomcat"
    }
    compile "org.springframework.boot:spring-boot-starter-jetty"
    compile "org.springframework.boot:spring-boot-starter-actuator"
    compile 'org.springframework.hateoas:spring-hateoas'
    compile 'org.springframework.security:spring-security-web'
    compile 'org.springframework.security:spring-security-config'
    compile "org.springframework.boot:spring-boot-starter-data-jpa"
    compile 'org.springframework.data:spring-data-rest-webmvc'
    compile 'org.springframework.data:spring-data-rest-hal-browser'
    
    runtime "org.hsqldb:hsqldb"

    testCompile "junit:junit"
    testCompile 'org.mockito:mockito-core'

    integrationTestCompile configurations.compile
    integrationTestRuntime configurations.runtime
    integrationTestCompile 'junit:junit'
    integrationTestCompile 'org.springframework.boot:spring-boot-starter-test'
}

task integrationTest(type: Test) {
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
}

check.dependsOn integrationTest


